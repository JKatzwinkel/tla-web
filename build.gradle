plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '1.1.0'
    id 'de.undercouch.download' version '4.1.1'
    id 'org.springframework.boot' version '2.4.3'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.0'
}

group = 'org.bbaw.aaew.tla'
version = '0.0.811'
sourceCompatibility = '11'

ext {
    fontawesomeVersion = env.FONTAWESOME_VERSION.orElse('5.12.1')
    bootstrapVersion = env.BOOTSTRAP_VERSION.orElse('4.4.1')
    jqueryVersion = env.JQUERY_VERSION.orElse('3.5.1')
    cookieJsVersion = env.COOKIEJS_VERSION.orElse('2.2.1')
    fontawesomeSrc = "https://use.fontawesome.com/releases/v${fontawesomeVersion}/fontawesome-free-${fontawesomeVersion}-web.zip"
    bootstrapSrc = "https://github.com/twbs/bootstrap/releases/download/v${bootstrapVersion}/bootstrap-${bootstrapVersion}-dist.zip"
    jquerySrc = "https://code.jquery.com/jquery-${jqueryVersion}.min.js"
    cookieJsSrc = "https://github.com/js-cookie/js-cookie/releases/download/v${cookieJsVersion}/js.cookie-${cookieJsVersion}.min.js"
    staticDir = 'src/main/resources/static'
}

def assets = [
  'cookieJs' : [cookieJsSrc, 'js.cookie.min.js'],
  'jquery': [jquerySrc, 'jquery.min.js'],
  'headroomJs': ['https://unpkg.com/headroom.js/dist/headroom.min.js', 'headroom.min.js']
]

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-web'
            pom {
                name = 'TLA Web Frontend'
                description = 'HTML frontend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spring.io/milestone' }
}

configurations {
    // don't embed all the servlet containers 
    implementation.exclude module: 'spring-boot-starter-jetty'
    implementation.exclude module: 'spring-boot-starter-netty'

    testImplementation.exclude group: 'org.junit.vintage'
    testImplementation.exclude module: 'junit-vintage-engine'
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.18'
    annotationProcessor 'org.projectlombok:lombok:1.18.18'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.18'

    implementation 'org.modelmapper:modelmapper:2.3.9'
    implementation 'org.jooq:jool:0.9.14'

    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.0-M2'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.5.0-M2'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.5.2'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha1'
    implementation 'org.apache.commons:commons-lang3:3.11'

    implementation 'com.github.jkatzwinkel:tla-common:query-expansion-SNAPSHOT'
    implementation 'com.github.rosmord.jsesh:jsesh:release-7.5.5'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.0-M2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0-M1'
}

application {
    mainClass = 'tla.web.App'
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    finalizedBy 'generateJacocoBadge'
}

task installAssets {
    description = 'Download and install third-party JS/CSS frameworks and libraries'
    dependsOn 'installBootstrap'
    dependsOn 'installFontAwesome'
    dependsOn 'installFontRoboto'
    outputs.files(
        file("${staticDir}/vendor/")
    )
}

def createDownloadAssetTask(String name, String url, String destFile) {
  return tasks.create("download${name.capitalize()}", Download) {
    doFirst {
      mkdir "${staticDir}/vendor"
    }
    description = "Download && install ${name}"
    src "${url}"
    dest file("${staticDir}/vendor/${destFile}")
    onlyIfModified true
  }
}

assets.each{
  entry -> installAssets.dependsOn createDownloadAssetTask(
    entry.key,
    entry.value[0],
    entry.value[1]
  )
}

task downloadFontAwesome(type: Download) {
    description = 'Downloads fontawesome distribution for local hosting'
    src fontawesomeSrc
    dest new File(buildDir, 'fontawesome.zip')
    onlyIfModified true
}

task downloadBootstrap(type: Download) {
    description = 'Downloads bootstrap distribution for local hosting'
    src bootstrapSrc
    dest new File(buildDir, 'bootstrap.zip')
    onlyIfModified true
}

task downloadFontRoboto(type: Download) {
    group = 'Init'
    description = 'Download Google Roboto font'
    src 'https://google-webfonts-helper.herokuapp.com/api/fonts/roboto?download=zip&subsets=latin&variants=300,500,700,300italic,regular,italic,700italic'
    dest new File(buildDir, 'roboto.zip')
    onlyIfModified true
}

task installFontAwesome(dependsOn: downloadFontAwesome, type: Copy) {
    group = 'Init'
    description = 'Installs fontawesome distribution into assets directory'
    from zipTree(downloadFontAwesome.dest)
    into file("${staticDir}/vendor")
    doLast {
      file("${staticDir}/vendor/fontawesome-free-${fontawesomeVersion}-web").renameTo(
        file("${staticDir}/vendor/fontawesome")
      )
    }
}

task installBootstrap(dependsOn: downloadBootstrap, type: Copy) {
    group = 'Init'
    description = 'Installs bootstrap distribution into assets directory'
    from zipTree(downloadBootstrap.dest)
    into file("${staticDir}/vendor")
    doLast {
      file("${staticDir}/vendor/bootstrap-${bootstrapVersion}-dist").renameTo(
        file("${staticDir}/vendor/bootstrap")
      )
    }
}

task installFontRoboto(dependsOn: downloadFontRoboto, type: Copy) {
    group = 'Init'
    description = 'Installs Google Roboto font'
    from zipTree(downloadFontRoboto.dest)
    into file("${staticDir}/font")
}

task install {
    inputs.files(
      file('.env')
    )
    group = 'Init'
    description = 'Installs additional resources required for running the app'
    dependsOn 'installAssets'
    outputs.files(
      file("${staticDir}/vendor/")
    )
}

task cleanAssets(type: Delete) {
    group = 'Init'
    description = 'Delete files added with :installAssets'
    delete "${staticDir}/vendor/"
}

clean {
    dependsOn 'cleanInstall'
}

springBoot {
    buildInfo()
}

bootJar {
    dependsOn 'install'
}

bootRun {
    systemProperty 'spring.profiles.active', 'dev'
    doFirst {
      if ( !file("${staticDir}/vendor/").exists() ) {
        println ' ======================================'
        println ' !!!  Must run task :install first: !!!'
        println ' !!!                                !!!'
        println ' !!!     ./gradlew.bat install      !!!'
        println ' ======================================'
        throw new StopExecutionException()
      }
    }
}

