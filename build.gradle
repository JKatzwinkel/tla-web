plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '1.0.2'
    id 'de.undercouch.download' version '4.0.4'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.0'
}

group = 'org.bbaw.aaew.tla'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    fontawesomeVersion = env.FONTAWESOME_VERSION.orElse('5.12.1')
    bootstrapVersion = env.BOOTSTRAP_VERSION.orElse('4.4.1')
    fontawesomeSrc = 'https://use.fontawesome.com/releases/v%s/fontawesome-free-%s-web.zip'
    bootstrapSrc = 'https://github.com/twbs/bootstrap/releases/download/v%s/bootstrap-%s-dist.zip'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-web'
            pom {
                name = 'TLA Web Frontend'
                description = 'HTML frontend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://www.qenherkhopeshef.org/maven' }
}

configurations {
    // don't embed all the servlet containers 
    implementation.exclude module: 'spring-boot-starter-jetty'
    implementation.exclude module: 'spring-boot-starter-netty'

    testImplementation.exclude group: 'org.junit.vintage'
    testImplementation.exclude module: 'junit-vintage-engine'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'org.modelmapper:modelmapper:2.3.7'

    implementation 'org.springframework.boot:spring-boot-starter-web:2.3.0.M1'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.3.0.M3'
    implementation 'org.slf4j:slf4j-log4j12:2.0.0-alpha1'

    implementation 'com.github.jkatzwinkel:tla-common:master-SNAPSHOT'
    implementation 'com.github.rosmord:jsesh:7.3.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

application {
    mainClassName = 'tla.web.App'
}

test {
    useJUnitPlatform()
    environment "bootstrapVersion", bootstrapVersion
    environment "fontawesomeVersion", fontawesomeVersion 
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    finalizedBy 'generateJacocoBadge'
}

task downloadFontAwesome(type: Download) {
    group = 'Init'
    description = 'Downloads fontawesome distribution for local hosting'
    src String.format(fontawesomeSrc, fontawesomeVersion, fontawesomeVersion)
    dest new File(buildDir, 'fontawesome.zip')
    onlyIfModified true
}

task downloadBootstrap(type: Download) {
    group = 'Init'
    description = 'Downloads bootstrap distribution for local hosting'
    src String.format(bootstrapSrc, bootstrapVersion, bootstrapVersion)
    dest new File(buildDir, 'bootstrap.zip')
    onlyIfModified true
}

task installFontAwesome(dependsOn: downloadFontAwesome, type: Copy) {
    group = 'Init'
    description = 'Installs fontawesome distribution into assets directory'
    from zipTree(downloadFontAwesome.dest)
    into new File('src/main/resources/static/vendor/')
}

task installBootstrap(dependsOn: downloadBootstrap, type: Copy) {
    group = 'Init'
    description = 'Installs bootstrap distribution into assets directory'
    from zipTree(downloadBootstrap.dest)
    into new File('src/main/resources/static/vendor/')
}

task installAssets {
    group = 'Init'
    description = 'Download and install bootstrap and fontawesome'
    dependsOn 'installBootstrap'
    dependsOn 'installFontAwesome'
}

bootRun {
    environment "bootstrapVersion", bootstrapVersion
    environment "fontawesomeVersion", fontawesomeVersion 
}
